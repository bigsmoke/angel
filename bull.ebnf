bull = { path } ;
path = dynamic path | static path ;
static path = heavy path | light path ;
dynamic path = heavy command | light command ;
heavy path = '[[' , steps , ']]' ;
light path = '[' , steps , ']' ;
heavy command = '((' , steps , '))' ;
light command = '(' , steps , ')' ;
steps = angel , [ step left , [ link ] ] , [ step right , [ steps ] ] ;
link = asymmetric link | symmetric link | knot ;
asymmetric link = { angel , point right , [ define | undefine ] } ,
                  [ heavy knot ] , 
                  { [ define | undefine ] , point left , angel } ;
                  (* Do something different with heavy knots? *)
                  (* Actually make at least one point obligatory! *)
symmetric link = point left , angel , point right ;
point right = light point right | heavy point right ;
point left = light point left | heavy point left ;
light point right = '>' ;
heavy point right = '>>' ;
light point left = '<' ;
heavy point left = '<<' ;
step left = light step left | heavy step left ;
step right = light step right | heavy step right ;
light step left = '\' ;
heavy step left = '\\' ;
light step right = '/' ;
heavy step right = '//' ;
light step up = '|' ;
heavy step up = '||' ;
heavy knot = '&&' ;
knot = '&' ;
angel = [ name ] , ( unknown | test | superpath | define | undefine | caller | argument | filter | count | length ) ;
name = { alphanumeric } , { alphanumeric | '-' | '.' | ',' | WS } ;
unknown = '?' ;
test = '?' , [ static path ] , '?' ;
superpath = '^' ;
define = '!' ;
undefine = '-!' ;
caller = '^@' ;
argument = '@' ;
filter = '*' ;
count = '#' ;
length = '#' , digit , { digit } , [ '.' , digit , { digit } ] ;
WS = white space ;
white space = ' ' | '\t' | '\n' , { white space } ;
alphanumeric = letter | digit ;
letter = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R"
       | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
       | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"  ;
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
(* vim: set filetype=ebnf: *)
